import { Steps, Tabs, Callout } from 'nextra/components'
import Image from 'next/image'

# Getting Started

Welcome to Quint! In this short guide, we'll cover everything from installation to your very first verification.

<Steps>
### Install Quint

<Tabs items={['NPM', 'Nix', 'Binaries']}>
 <Tabs.Tab className="mt-3">
  Install [node/npm](https://nodejs.org/en/download/package-manager) if you don't have it already. Then, open your terminal and run:

  ```sh
  npm i @informalsystems/quint -g
  ```

 </Tabs.Tab>
 <Tabs.Tab>
 If you have [Nix](https://nixos.org/) installed, you can obtain a shell with Quint and all its dependencies installed by running:

 ```sh
 nix-shell -p quint
 ```

 Note that the release cycle with Nix is slower than with other alternatives, so latest features might not be available.

 The Nix package is a great option to use in the CI.
 </Tabs.Tab>
 <Tabs.Tab>

  Download the binaries directly from [GitHub Releases](https://github.com/informalsystems/quint/releases).

 </Tabs.Tab>
</Tabs>

### Setup your code editor

For the best experience writing Quint, you should set up your code editor with the Quint tools. The VSCode setup is, by far, the easiest, so you might want to use it if you want a quick start.

<Tabs items={['VSCode', 'Emacs', 'Vim', 'Helix']}>
 <Tabs.Tab>
<Image src="/vscode.png" alt="Quint extension on VSCode" width={500} height={100} />
 Install the [Quint extension](https://marketplace.visualstudio.com/items?itemName=informal.quint-vscode):
 1. Open the extensions panel by pressing `Ctrl+Shift+X` or clicking the extensions icon.
 2. Search for Quint and click Install.
 </Tabs.Tab>
 <Tabs.Tab>
 We have 2 packages for enabling support for Quint in Emacs.

  1. `quint-mode` is a major mode that enables simple syntax highlighting
  2. `lsp-quint` is a client for `lsp-mode`, enabling IDE features provided by `quint-language-server`

  These packages are not published anywhere for the moment. Get the files from [Quint's git repository](https://github.com/informalsystems/quint/tree/main/editor-plugins/emacs) (i.e. by cloning it) and then add a configuration like the following (which uses `use-package`):

  ```elisp
  (load-file "<path-to-quint-repo>/editor-plugins/emacs/quint-mode.el")
  (load-file "<path-to-quint-repo>/editor-plugins/emacs/lsp-quint.el")
  (require 'quint-mode)
  (add-to-list 'auto-mode-alist '("\\.qnt" . quint-mode))
  (use-package lsp-quint
    :ensure t
    :hook (quint-mode . lsp))
  ```
 </Tabs.Tab>
 <Tabs.Tab>

 First, you'll need to install the language server via npm:
 ```sh
 npm i @informalsystems/quint-language-server -g
 ```

 For Neovim:
 1. Download the [quint.vim](https://github.com/informalsystems/quint/blob/main/editor-plugins/vim/quint.vim) file and move it to `~/.config/nvim/syntax`
 2. Enable automatic syntax highlighting and language server integration by adding the following lines to your `~/.config/nvim/init.vim`:
```vim-script
autocmd FileType quint lua vim.lsp.start({name = 'quint', cmd = {'quint-language-server', '--stdio'}, root_dir = vim.fs.dirname()})
au BufRead,BufNewFile *.qnt  setfiletype quint
au BufNewFile,BufReadPost *.qnt runtime syntax/quint.vim
```

 For Vim, follow the extended instructions in [here](https://github.com/informalsystems/quint/tree/main/editor-plugins/vim).
 </Tabs.Tab>
 <Tabs.Tab>

  ðŸŽ‰ Helix has built-in support for Quint, you don't need to do anything!

 </Tabs.Tab>
</Tabs>

### Write a specification

In order to run Quint, we first need a specification. Let's use this simple bank specification, which has a bug.

```quint
module bank {
  /// A state variable to store the balance of each account
  var balances: str -> int

  pure val ADDRESSES = Set("alice", "bob", "charlie")

  action deposit(account, amount) = {
    // Increment balance of account by amount
    balances' = balances.setBy(account, curr => curr + amount)
  }

  action withdraw(account, amount) = {
    // Decrement balance of account by amount
    balances' = balances.setBy(account, curr => curr - amount)
  }

  action init = {
    // At the initial state, all balances are zero
    balances' = ADDRESSES.mapBy(_ => 0)
  }

  action step = {
    // Non-deterministically pick an address and an amount
    nondet account = ADDRESSES.oneOf()
    nondet amount = 1.to(100).oneOf()
    // Non-deterministically choose to either deposit or withdraw
    any {
      deposit(account, amount),
      withdraw(account, amount),
    }
  }

  /// An invariant stating that all accounts should have a non-negative balance
  val no_negatives = ADDRESSES.forall(addr => balances.get(addr) >= 0)
}
```

Create a `bank.qnt` file and add the contents above to it. Then, let's try to check the invariant `no_negatives`. This invariant states that none of the balances should be less then zero, ever. An invariant is something that needs to be true for all reachable states.

### Find a violation

To look for violations of this invariant, we can use the `run` subcommand, which will simulate a bunch of executions while checking the invariant. In a terminal, while on the directory of this file, execute:
```sh
quint run bank.qnt --invariant=no_negatives
```

This should result in a violation. You can inspect the balances and find a negative value. If you want to understand better how we got to such state, try using the `--mbt` flag, which includes additional metadata on the violation trace (usually used for testing purposes. MBT stands for Model Based Testing):
```sh
quint run bank.qnt --invariant=no_negatives --mbt
```

Now, you should see that a withdraw action was taken right before the balances went negative.

### Fix the issue

Let's update the `withdraw` definition to prevent this scenario - users should not be able to withdraw more than they currently have.

```quint
action withdraw(account, amount) = all { // <- all of the following need to hold
  // A precondition, there should be enough to withdraw
  balances.get(account) >= amount,
  // Decrement balance of account by amount
  balances' = balances.setBy(account, curr => curr - amount),
}
```

We can run the simulator again. This time, it should find no violation and return an `[ok]` result:
```sh
quint run bank.qnt --invariant=no_negatives
```

### Verify the result

However, the simulator might miss several executions. To make sure we fixed the problem, we should run the model checker, by using the `verify` subcommand.

<Callout>
The model checker requires Java Development Kit >= 17. We recommend version 17 of the [Eclipse Temurin](https://adoptium.net/) or [Zulu](https://www.azul.com/downloads/?version=java-17-lts&package=jdk#download-openjdk) builds of OpenJDK.
</Callout>
```sh
quint verify bank.qnt --invariant=no_negatives
```

This will verify all possible executions of up to 10 steps. We can be confident that we fixed the issue after seeing the `[ok]` result from the `verify` command.
</Steps>

That's it! Now that you have the tools and know the workflow, you might want to learn how to write your own specs! Check out the Quint [Lessons](/docs/lessons) and [Examples â†—](https://github.com/informalsystems/quint/tree/main/examples).
